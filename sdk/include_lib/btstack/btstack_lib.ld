SECTIONS
{
    .data : ALIGN(4)
    {
        btstack_data_start = .;
        *(.bt_stack_data)
        *(.ble_db_data)
        *(.ble_sm_data)
        *(.ble_att_data)
        *(.ble_gatt_data)
        *(.fmna_stack_data)
        *(.fmna_plaform_data)
        *(.fmna_crypto_data)

	/*mesh*/
        BTSTACK_LE_HOST_MESH_DATA_START = .;
	. = (. +3) & ~ 3;
        _net_buf_pool_list = .;
        *(._net_buf_pool.static.*)

        *(.ble_mesh_data)
        *(.ble_mesh_tinycrypt_data)
        BTSTACK_LE_HOST_MESH_DATA_SIZE = ABSOLUTE(. - BTSTACK_LE_HOST_MESH_DATA_START);

	/*Le audio big*/
        BTSTACK_LE_AUDIO_BIG_DATA_START = .;
        *(.big_rx_data)
        *(.big_tx_data)
        *(.big_common_data)
        BTSTACK_LE_AUDIO_BIG_DATA_SIZE = ABSOLUTE(. - BTSTACK_LE_AUDIO_BIG_DATA_START);

        btstack_data_end = .;

        . = ALIGN(4);
    } > ram0

    .bss (NOLOAD) :ALIGN(4)
    {
        btstack_bss_start = .;
        *(.bt_stack_bss)
        *(.ble_db_bss)
        *(.ble_sm_bss)
        *(.ble_att_bss)
        *(.ble_gatt_bss)
        *(.btstack_pool)
        *(.fmna_stack_bss)
        *(.fmna_plaform_bss)
        *(.fmna_crypto_bss)

	/*mesh*/
        BTSTACK_LE_HOST_MESH_BSS_START = .;
        *(.ble_mesh_bss)
        *(.ble_mesh_tinycrypt_bss)
        BTSTACK_LE_HOST_MESH_BSS_SIZE = ABSOLUTE(. - BTSTACK_LE_HOST_MESH_BSS_START);

	/*Le audio big*/
        BTSTACK_LE_AUDIO_BIG_BSS_START = .;
        *(.big_rx_bss)
        *(.big_tx_bss)
        *(.big_common_bss)
        BTSTACK_LE_AUDIO_BIG_BSS_SIZE = ABSOLUTE(. - BTSTACK_LE_AUDIO_BIG_BSS_START);

        btstack_bss_end = .;
        . = ALIGN(4);
    } > ram0

    .text : ALIGN(4)
    {
        btstack_code_start = .;

        . = ALIGN(4);

        a2dp_source_media_codec_begin = .;
        KEEP(*(.a2dp_source_media_codec))
        a2dp_source_media_codec_end = .;
        a2dp_sink_media_probe_begin = .;
        KEEP(*(.a2dp_sink_media_probe))
        a2dp_sink_media_probe_end = .;

        a2dp_sink_media_codec_begin = .;
        KEEP(*(.a2dp_sink_media_codec))
        a2dp_sink_media_codec_end = .;

        a2dp_event_handler_begin = .;
        KEEP(*(.a2dp_event_handler))
        a2dp_event_handler_end = .;

        sdp_record_item_begin = .;
        KEEP(*(.sdp_record_item))
        sdp_record_item_end = .;

        bt_sleep_begin = .;
        KEEP(*(.bt_sleep))
        bt_sleep_end = .;

	*(.bt_stack_const)
	*(.bt_stack_code)
        *(.ble_db_const)
        *(.ble_db_code)
        *(.ble_sm_const)
        *(.ble_sm_code)
        *(.ble_att_const)
        *(.ble_att_code)
        *(.ble_gatt_const)
        *(.ble_gatt_code)
        *(.fmna_stack_const)
        *(.fmna_stack_code)
        *(.fmna_plaform_const)
        *(.fmna_plaform_code)
        *(.fmna_crypto_const)
        *(.fmna_crypto_code)

	/*mesh*/
        BTSTACK_LE_HOST_MESH_CODE_START = .;
        *(.ble_mesh_code)
        *(.ble_mesh_tinycrypt_code)

        *(.ble_mesh_const)
        *(.ble_mesh_tinycrypt_const)
        BTSTACK_LE_HOST_MESH_CODE_SIZE = ABSOLUTE(. - BTSTACK_LE_HOST_MESH_CODE_START);

	/*Le audio big*/
        BTSTACK_LE_AUDIO_BIG_TEXT_START = .;
        *(.big_rx_const)
        *(.big_tx_const)
        *(.big_common_const)

        *(.big_rx_code)
        *(.big_tx_code)
        *(.big_common_code)
        BTSTACK_LE_AUDIO_BIG_TEXT_SIZE = ABSOLUTE(. - BTSTACK_LE_AUDIO_BIG_TEXT_START);

        btstack_code_end = .;
	. = ALIGN(4);
    } > code0
}

BTSTACK_LE_HOST_MESH_RAM_TOTAL = BTSTACK_LE_HOST_MESH_DATA_SIZE + BTSTACK_LE_HOST_MESH_BSS_SIZE;
BTSTACK_LE_HOST_MESH_FLASH_TOTAL = BTSTACK_LE_HOST_MESH_CODE_SIZE;

BTSTACK_CODE_SIZE = (btstack_code_end - btstack_code_start) + (btstack_data_end - btstack_data_start);

